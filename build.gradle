plugins {
    id 'base'
}

repositories {
    maven {
        url "https://maven.pkg.github.com/hiconic-os/maven-repo-dev"
        credentials {
            username 'not-needed'
            password System.getenv('GITHUB_READ_PACKAGES_TOKEN')
        }
    }
}

configurations {
    customArtifact {
        transitive = false
    }
}

dependencies {
    customArtifact 'tribefire.extension.setup:devrock-sdk:2.1.+@zip'
}

task downloadDevrockSdk(type: Copy) {
    from configurations.customArtifact
    into "build"
    rename { fileName -> "devrock-sdk.zip" }
}

task buildBaseImage(type: Exec) {
    def branch = project.hasProperty("branch") ? project.branch : "main";
    commandLine ('docker', 'build', 
        "-t", "ghcr.io/hiconic-os/ci-base/${branch}:latest",
        "-f", "Dockerfile-base",
        "."
    )
}

task buildDevrockImage(type: Exec) {
    def branch = project.hasProperty("branch") ? project.branch : "main";
    commandLine ('docker', 'build', 
        "--build-arg", "GITHUB_READ_PACKAGES_TOKEN=${System.getenv('GITHUB_READ_PACKAGES_TOKEN')}",
        "-t", "ghcr.io/hiconic-os/ci-devrock-sdk/${branch}:latest",
        "-f", "Dockerfile-devrock-sdk",
        "."
    )
}

task buildAll(type: GradleBuild) {
    tasks = ['downloadDevrockSdk', 'buildBaseImage', 'buildDevrockImage']
}
