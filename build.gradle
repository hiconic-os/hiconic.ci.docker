plugins {
    id 'base'
}

repositories {
    maven {
        url "https://maven.pkg.github.com/hiconic-os/maven-repo-dev"
        credentials {
            username 'not-needed'
            password System.getenv('GITHUB_READ_PACKAGES_TOKEN')
        }
    }
}

configurations {
    hiconicSdk {
        transitive = false
    }
}

dependencies {
    hiconicSdk 'tribefire.extension.setup:hiconic-sdk:2.1.+@zip'
}

task downloadHiconicSdk(type: Copy) {
    from configurations.hiconicSdk
    into "build"
    rename { fileName -> "hiconic-sdk.zip" }
}

task buildBaseImage(type: Exec) {
    def branch = project.hasProperty("branch") ? project.branch : "main";
    commandLine ('docker', 'build', 
        "-t", "ghcr.io/hiconic-os/ci-base/${branch}:latest",
        "-f", "Dockerfile-base",
        "."
    )
}

task buildHiconicImage(type: Exec) {
    def branch = project.hasProperty("branch") ? project.branch : "main";
    commandLine ('docker', 'build', 
        "-t", "ghcr.io/hiconic-os/ci-hiconic-sdk/${branch}:latest",
        "-f", "Dockerfile-hiconic-sdk",
        "."
    )
}

task buildAll(type: GradleBuild) {
    tasks = ['downloadHiconicSdk', 'buildBaseImage', 'buildHiconicImage']
}
